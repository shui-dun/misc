" 删除space在普通和可视模式下的映射
nnoremap <Space> <Nop>
vnoremap <Space> <Nop>
" 设置前缀键（leader键）为空格
let mapleader="\<Space>"
" 一行太长时能显示多少就显示多少
set display+=lastline
" 显示ruler
set ruler
set rulerformat=%30(%{&fileformat}\|%{\"\".(\"\"?&enc:&fenc).((exists(\"+bomb\")\ &&\ &bomb)?\"+\":\"\").\"\"}\ %l,%c%V\ %LL%)
" 显示行号
set number
" 允许在不保存当前文件时切换到另一个缓冲区
set hidden
" 设置检测文件编码的顺序
set fileencodings=utf-8,gb2312,gbk
" indent折叠（使用语法折叠可能会影响性能，并且需要针对各种语言手动开启）
set foldmethod=indent
" 默认内容都是展开的
set foldlevelstart=99
" markdown语法折叠
let g:markdown_folding = 1
" L和H用来快速水平移动
function! MyLMapping()
    if &wrap
        return "gj"
    else
        return "zL"
    endif
endfunction
nnoremap <expr> L MyLMapping()
vnoremap <expr> L MyLMapping()
function! MyHMapping()
    if &wrap
        return "gk"
    else
        return "zH"
    endif
endfunction
nnoremap <expr> H MyHMapping()
vnoremap <expr> H MyHMapping()
" J和K快速垂直移动
nnoremap <expr> J (winheight(0) - 1) . 'gjzb'
vnoremap <expr> J (winheight(0) - 1) . 'gjzb'
nnoremap <expr> K (winheight(0) - 1) . 'gkzt'
vnoremap <expr> K (winheight(0) - 1) . 'gkzt'
" 显示命令
set showcmd
" 设置保存的历史命令数目
set history=1000
" 输入命令时按下tab键自动补全
set wildmenu
set wildmode=longest:full,full
set wildoptions=pum
" 搜索时忽略大小写
set ignorecase
" 如果搜索中有一个或以上的大写字母，则不忽略大小写
set smartcase
" 搜索时高亮显示匹配的文本
set hlsearch
" 可视模式下搜索，自动搜索选中的文本
vnoremap / y/\V<C-R>"<CR>
vnoremap <silent> * :<C-u>call VisualSelection('f')<CR>/<C-R>=@/<CR><CR>
" 将?映射到使用列表展示全部搜索结果
function! SearchAndListResults()
    " 首先，获取之前正在搜索的单词，也就是@/的值
    let search_word = @/
    " silent表示不显示'press enter to continue'这种命令输出
    " vimgrep表示使用列表展示搜索结果
    " copen表示打开搜索结果列表（为什么vim不能自动弹出列表呢？很不人性化）
    let cmd = "silent vimgrep /" . search_word . "/ % | copen"
    execute cmd
endfunction
nnoremap ? :call SearchAndListResults()<CR>
" 按F12切换粘贴模式，避免粘贴时自动缩进，以及tab被转化为空格
set pastetoggle=<F12>
" 每行的缩进值和上一行相等
set autoindent
" tab显示出来是多少个空格的长度
set tabstop=4
" 按tab时缩进的长度
set shiftwidth=4
" 按退格键的时候退回缩进的长度
set softtabstop=4
" 缩进用空格来表示
set expandtab
function! DetectIndent()
    " 只检测文件的前N行来提高效率，避免在大文件中造成延迟
    let l:maxLines = min([line('$'), 100])

    for i in range(1, l:maxLines)
        let l:line = getline(i)
        " 检测tab风格的缩进
        if l:line =~ '^\t'
            set tabstop=4
            set shiftwidth=4
            set softtabstop=4
            set noexpandtab
            return
        " 检测2个空格风格的缩进
        elseif l:line =~ '^  \S'
            set tabstop=2
            set shiftwidth=2
            set softtabstop=2
            set expandtab
            return
        endif
    endfor
endfunction
" 只针对小文件进行缩进检测
autocmd BufReadPost * if getfsize(bufname('%')) < 32*1024*1024 | call DetectIndent() | endif
" 可视化tab等特殊字符
set list
set listchars=tab:>-,trail:.
" 设置补全(ctrl+p)时应该查找哪些类型的候选项，以及如何排序
set cpt=.,k,w,b
" vim的自带目录工具netrw默认使用树视图
let g:netrw_liststyle=3
" 隐藏netrw的帮助信息
let g:netrw_banner=0
" 设置netrw的浏览方式为水平分割
let g:netrw_browse_split=2
" 设置netrw的分割窗口的宽度
let g:netrw_winsize=50
" 打开netrw快捷键
nnoremap <leader>e :Vexplore<CR>
" 普通模式下的x和X是删除而不是剪切
nnoremap x "_x
nnoremap X "_X
" 为d和x提供删除版本的快捷键
nnoremap D "_d
vnoremap D "_d
vnoremap X "_x
" 让剪切板寄存器 "+ 与 "* 和匿名寄存器 "" 保持同步
set clipboard=unnamed,unnamedplus
" 比较当前编辑但没保存的文件和磁盘上的文件
function! s:DiffWithSaved()
    let filetype=&ft
    diffthis
    vnew | r # | normal! 1Gdd
    diffthis
    exe "setlocal bt=nofile bh=wipe nobl noswf ro ft=" . filetype
endfunction
com! DiffSaved call s:DiffWithSaved()
" 搜索时默认不使用正则表达式
nnoremap / /\V
" leader+r替换，默认不使用正则表达式
nnoremap <leader>r :%s/\V//gc<left><left><left><left>
" 统计字数（包含中文字符和英文单词）
function! WordCnt() range
    let save = @z
    silent exec 'normal! gv"zy'
    let text = @z
    let @z = save
    silent exec 'normal! gv'

    " 统计中文字符数目
    let cc = 0
    for char in split(text, '\zs')
        if char2nr(char) >= 0x2000
            let cc += 1
        endif
    endfor

    " 移除选中文本中的中文字符（但这一步太耗时了）
    " let engText = substitute(text, '[\u4e00-\u9fff]', '', 'g')
    " 统计单词数目（按照空格和各种标点符号以及换行符分割）
    let wordCount = len(split(text, '[[:blank:][:punct:][:cntrl:]]\+'))
    echo "Count of Chinese characters is:" cc
    echo "Count of words is:" wordCount
endfunc
command! -range WordCnt <line1>,<line2>call WordCnt()
" 细粒度的undo
inoremap , ,<c-g>u
inoremap . .<c-g>u
inoremap ( (<c-g>u
inoremap [ [<c-g>u
inoremap = =<c-g>u
inoremap <space> <space><c-g>u
" 回车应用补全
inoremap <silent><expr> <CR> pumvisible() ? "\<C-Y>" : "\<CR>\<C-g>u"
inoremap ， ，<c-g>u
inoremap 。 。<c-g>u
" 修复不能退格的bug
set backspace=indent,eol,start
" 增强diff
set diffopt+=algorithm:histogram,indent-heuristic
" >和<自动恢复可视化区域
vnoremap < <gv
vnoremap > >gv
" 对大文件的设置
autocmd BufReadPre * if getfsize(bufname('%')) > 32*1024*1024
    \ | syntax off
    \ | setlocal foldmethod=manual
    \ | setlocal nowrap
    \ | endif


" 插件管理
call plug#begin()
" copilot补全，默认不启用
Plug 'github/copilot.vim', { 'on': [] }
" 常见语言的词典
Plug 'skywind3000/vim-dict'
" 模糊查找
Plug 'junegunn/fzf'
Plug 'junegunn/fzf.vim'
" vim初始页
Plug 'mhinz/vim-startify'
" 快速跳转
Plug 'easymotion/vim-easymotion'
" 注释
Plug 'preservim/nerdcommenter'
" 查看文件tag
Plug 'preservim/tagbar'
" 代码补全
Plug 'neoclide/coc.nvim', {'branch': 'release'}
" undo历史
Plug 'mbbill/undotree'
call plug#end()

" copilot配置
" 补全所有文件类型
let g:copilot_filetypes = {
\ '*': v:true,
\ }
" 设置启用copilot的快捷键
nnoremap <leader>p :call plug#load("copilot.vim")<CR>

" fzf.vim配置
" leader+h打开命令历史记录（:History:）
nnoremap <leader>h :History:<CR>
" leader+f打开最近文件（:History）
nnoremap <leader>f :History<CR>
" leader+t打开当前文件的标签（:BTags）
nnoremap <leader>t :BTags<CR>
" leader+F打开文件搜索（:Files）
nnoremap <leader>F :Files<CR>
" leader+b打开缓冲区列表（:Buffers）
nnoremap <leader>b :Buffers<CR>
" leader+g打开rg搜索（:Rg）
nnoremap <leader>g :Rg<CR>
" leader+G打开ag搜索（:Ag）
nnoremap <leader>G :Ag<CR>

" startify配置
let g:startify_custom_header = [
			\'    █   █  ▀  █▀▄▀█     .-. ________  ',
			\'     █ █   █▀ █ ▀ █     |=|/_____/  \ ',
			\'     ▀▄▀  ▄█▄ █   █     |_|_[X]_|_""_|',
			\]
" 列表顺序
let g:startify_lists = [
          \ { 'type': 'files',     'header': ['   MRU']            },
          \ { 'type': 'sessions',  'header': ['   Sessions']       },
          \ { 'type': 'bookmarks', 'header': ['   Bookmarks']      },
          \ { 'type': 'commands',  'header': ['   Commands']       },
          \ { 'type': 'dir',       'header': ['   MRU '. getcwd()] },
          \ ]

" easymotion配置
" 禁用所有默认的快捷键
let g:EasyMotion_do_mapping = 0
" 智能大小写
let g:EasyMotion_smartcase = 1
" 设置根据字符搜索的快捷键为s
map s <Plug>(easymotion-s)
" 设置行跳转的快捷键
map t <Plug>(easymotion-bd-jk)

" nerdcommenter配置
" 禁用默认的快捷键
let g:NERDCreateDefaultMappings = 0
" 检查所有选定的行是否已被注释（只要有一行没有被注释，就添加注释而非删除注释）
let g:NERDToggleCheckAllLines = 1
" 设置toggle注释的快捷键
nnoremap <leader>/ V<Plug>NERDCommenterToggle
vnoremap <leader>/ V<Plug>NERDCommenterToggle
" 注释之后加一个空格
let g:NERDSpaceDelims = 1
" python自动会多加一个空格
au FileType python let g:NERDSpaceDelims = 0
" 设置注释的对齐方式是左对齐
let g:NERDDefaultAlign = 'left'

" tagbar配置
" 设置tagbar的快捷键
nnoremap <leader>s :TagbarToggle<CR>
" 阻止'press ENTER to continue'的出现
" let g:tagbar_silent = 1

" coc.nvim配置
set nobackup
set nowritebackup
set updatetime=300
set signcolumn=no
" 使用回车补全，使用ctrl+n/p选择
function! MyEnterMapping()
    if coc#pum#visible()
        return coc#pum#confirm()
    elseif pumvisible()
        return "\<C-y>"
    else
        return "\<C-g>u\<CR>\<c-r>=coc#on_enter()\<CR>"
    endif
endfunction
inoremap <silent><expr> <CR> MyEnterMapping()
" navigate diagnostics
nmap <silent> g[ <Plug>(coc-diagnostic-prev)
nmap <silent> g] <Plug>(coc-diagnostic-next)
" GoTo code navigation
nmap <silent> gd <Plug>(coc-definition)
nmap <silent> gy <Plug>(coc-type-definition)
nmap <silent> gi <Plug>(coc-implementation)
nmap <silent> gr <Plug>(coc-references)
" show documentation in preview window
nnoremap <silent> <leader>cd :call ShowDocumentation()<CR>
function! ShowDocumentation()
    if CocAction('hasProvider', 'hover')
        call CocActionAsync('doHover')
    endif
endfunction
" Highlight the symbol and its references when holding the cursor
autocmd CursorHold * silent call CocActionAsync('highlight')
" Symbol renaming
nmap <leader>cr <Plug>(coc-rename)
" Formatting selected code
xmap <leader>cf  <Plug>(coc-format-selected)
" Applying code actions to the selected code block
nmap <leader>ca  <Plug>(coc-codeaction-cursor)
" 默认禁止启动coc（太占内存了）
let g:coc_start_at_startup=0
" 手动启动coc
nnoremap <leader>cc :CocStart<CR>

" undotree配置
nnoremap <leader>u :UndotreeToggle<CR>

" windows下gvim的配置
if has('gui_running') && (has('win32') || has('win64'))
    " 禁止swap文件（因为本机不像ssh存在断连风险）
    set noswapfile
    " 设置gui下的字体
    " 这个字体需要去https://github.com/crvdgc/Consolas-with-Yahei安装
    set guifont=Consolas-with-Yahei:h13
    " 主题
    colorscheme morning
    " 删除菜单栏
    set guioptions-=m
    " 删除工具栏
    set guioptions-=T
    " 删除右侧垂直滚动条
    set guioptions-=r
    " 删除左侧水平滚动条
    set guioptions-=L
    " 删除底部状态栏
    set guioptions-=b
    " 启动时最大化
    au GUIEnter * simalt ~x
    " 禁止光标闪烁
    set guicursor+=a:blinkon0
    " ctrl+c复制
    vnoremap <C-c> "+y
    " ctrl+v粘贴
    nnoremap <C-v> "+p
    vnoremap <C-v> "+p
    inoremap <C-v> <Esc>"+pa
    " ctrl+x剪切
    vnoremap <C-x> "+d
    " ctrl+z撤销
    nnoremap <C-z> u
    " ctrl+o是代表临时进入普通模式，执行完命令后自动回到插入模式
    inoremap <C-z> <C-o>u
    vnoremap <C-z> <Esc>u
    " ctrl+r重做
    inoremap <C-r> <C-o><C-r>
    vnoremap <C-r> <Esc><C-r>
    " ctrl+a全选
    nnoremap <C-a> ggVG
    inoremap <C-a> <Esc>ggVG
    vnoremap <C-a> <Esc>ggVG
    " ctrl+s保存
    nnoremap <C-s> :w<CR>
    inoremap <C-s> <Esc>:w<CR>a
    vnoremap <C-s> <Esc>:w<CR>
    " shift+home到行首
    nnoremap <S-Home> v^
    inoremap <S-Home> <Esc>v^
    vnoremap <S-Home> ^
    " shift+end到行尾
    nnoremap <S-End> v$
    inoremap <S-End> <Esc><Right>v$
    vnoremap <S-End> $
    " shift+pageup向上翻页
    nnoremap <S-PageUp> v10k
    inoremap <S-PageUp> <Esc>v10k
    vnoremap <S-PageUp> 10k
    " shift+pagedown向下翻页
    nnoremap <S-PageDown> v10j
    inoremap <S-PageDown> <Esc><Right>v10j
    vnoremap <S-PageDown> 10j
    " shift+up向上移动
    nnoremap <S-Up> vk
    inoremap <S-Up> <Esc>vk
    vnoremap <S-Up> k
    " shift+down向下移动
    nnoremap <S-Down> vj
    inoremap <S-Down> <Esc><Right>vj
    vnoremap <S-Down> j
    " shift+left向左移动
    nnoremap <S-Left> vh
    inoremap <S-Left> <Esc>vh
    vnoremap <S-Left> h
    " shift+right向右移动
    nnoremap <S-Right> vl
    inoremap <S-Right> <Esc><Right>vl
    vnoremap <S-Right> l
    " ctrl+left向左移动一个单词
    nnoremap <C-Left> b
    vnoremap <C-Left> b
    " ctrl+right向右移动一个单词
    nnoremap <C-Right> w
    vnoremap <C-Right> w
    " shif+ctrl+left向左移动一个单词
    nnoremap <S-C-Left> vb
    inoremap <S-C-Left> <Esc>vb
    vnoremap <S-C-Left> b
    " shif+ctrl+right向右移动一个单词
    nnoremap <S-C-Right> vw
    inoremap <S-C-Right> <Esc><Right>vw
    vnoremap <S-C-Right> w
    " 退格键删除
    nnoremap <BS> "_X
    vnoremap <BS> "_x
    " 删除键删除而非剪切
    nnoremap <Del> "_x
    vnoremap <Del> "_x
    " 使用ctrl+/toggle注释
    nnoremap <C-/> V<Plug>NERDCommenterToggle
    inoremap <C-/> <Esc>V<Plug>NERDCommenterToggle
    vnoremap <C-/> V<Plug>NERDCommenterToggle
    " 可视模式下tab和shift+tab缩进和反缩进
    vnoremap <Tab> >gv
    vnoremap <S-Tab> <gv
    " 打开explorer.exe快捷键（其中start是为了实现异步，即不阻塞vim进程，而silent是防止产生“请按enter或其他命令继续”的输出）
    nnoremap <leader>E :silent !start explorer %:h<CR>
    " 打开terminal快捷键（但是环境和直接打开powershell有些许不同，原因未知）
    nnoremap <leader>T :silent !start pwsh -WorkingDirectory %:h -NoExit -Command "$env:LANG='zh_CN.UTF-8'"<CR>

    " 插件相关配置
    " fzf配置
    " 禁止fzf的预览（避免中文导致windows下的fzf.vim渲染错误）
    let g:fzf_preview_window='right:0'
endif
